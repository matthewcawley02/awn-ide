AWNRoot := ws block=Block* $

//in a block, each entry must be separated by a linebreak, optional spacing at the start
//because each entry must be delimited with a linebreak, an extra line is required at the end of the doc currently

Block :=
	  pos=@ 'INCLUDES:' include=Include+
	| pos=@ 'include' include=Include
	| pos=@ 'TYPES:' type=Type+
	| pos=@ 'VARIABLES:' var=ConVar+
	| pos=@ 'CONSTANTS:' const=ConVar+
	| pos=@ 'FUNCTIONS:' func=Function+
	| pos=@ 'PROCESSES:' proc=Process+
	| pos=@ 'proc' proc=Process

Include := sp posS=@ name=Name posE=@ lb

Type := sp posS=@ typeName=TypeName posE=@ typeExprW={'\=' typeExpr=TE }? lb

ConVar :=
	  sp typeExpr=TE sp posS=@ nameFirst=Name posE=@ namesMore={',' posS=@ name=Name posE=@ }* lb
	| sp posS=@ name=Name posE=@ '\: ' typeExpr=TE lb

Function :=
	  sp posS=@ name=Name posE=@ '\: ' typeExpr=TE lb
	| sp name=Infix '\: ' binTypeExpr=BTE lb

Process :=
	  sp pos1S=@ nameFirst=Name pos1E=@ '\(' pos2S=@ argFirst=Name? pos2E=@ argsMore={',' posS=@ name=Name posE=@ }* '\)' '\:\=' lb sp proc=SPE lb
	| sp posS=@ name=Name posE=@ '\:\=' proc=SPE lb

//============
// TYPE EXPRESSIONS
//============

TE := '\(' typeExpr=TE '\)' typeExprMore=TE1?
	.typetype = string{
		return "brackets"
	}
	| pos=@ 'Pow' '\(' typeExpr=TE '\)' typeExprMore=TE1?
	.typetype = string{
		return "pow"
	}
	| '\[' typeExpr=TE '\]' typeExprMore=TE1?
	.typetype = string{
		return "list"
	}
	| posS=@ typename=TypeName posE=@ typeExprMore=TE1?
	.typetype = string{
		return "name"
	}

TE1 := '\-\>' typeExpr=TE typeExprMore=TE1?
	.typetype = string{
		return "function"
	}
	| '\+->' typeExpr=TE typeExprMore=TE1?
	.typetype = string{
		return "part_function"
	}
	| {pos=@ ' x ' typeExpr=TE}* typeExprMore=TE1?
	.typetype = string{
		return "product"
	}


// for l x r
//	.exprList = TE[]{
//		const l = [left]
//        l.push(...right.map(r => r.typeExpr));
//       return l
//	}

BTE :=
	  left=BTE_AUX ' x ' right=BTE_AUX '->' typeExpr=TE
	| left=BTE_AUX ' x ' right=BTE_AUX '\+->' typeExpr=TE

BTE_AUX :=
	  name=TypeName
	.typetype = string {
		return "name"
	}
	| '\(' typeExpr=TE '\)'
	.typetype = string {
		return "brackets"
	}
	| 'Pow' '\(' typeExpr=TE '\)'
	.typetype = string {
		return "pow"
	}
	| '\[' typeExpr=TE '\]'
	.typetype = string {
		return "list"
	}

//==================
// SP EXPRESSIONS
//==================

SPE :=
	  '\[' dataExp=DE '\]' proc=SPE procMore=SPE1?
	.procType = string {
		return "guard"
	}
	| '\[\[' posS=@ name=Name posE=@ dataExpList={'\[' dataExp=DE '\]' }* ':=' dataExpAssignment=DE '\]\]' lb sp proc=SPE procMore=SPE1?
	.procType = string {
		return "assignment"
	}
	| pos=@ 'unicast' '\(' dataExpL=DE ', ' dataExpR=DE '\)' '.' procL=SPE lb sp '>' procR=SPE procMore=SPE1?
	.procType = string {
		return "unicast"
	}
	| pos=@ 'broadcast' '\(' dataExp=DE '\)' '.' lb sp proc=SPE procMore=SPE1?
	.procType = string {
		return "broadcast"
	}
	| pos=@ 'groupcast' '\(' dataExpL=DE ', ' dataExpR=DE '\)' '.' lb sp proc=SPE procMore=SPE1?
	.procType = string {
		return "groupcast"
	}
	| pos=@ 'send' '\(' dataExp=DE '\)' '.' lb sp proc=SPE procMore=SPE1?
	.procType = string {
		return "send"
	}
	| pos=@ 'deliver' '\(' dataExp=DE '\)' '.' lb sp proc=SPE procMore=SPE1?
	.procType = string {
		return "deliver"
	}
	| pos=@ 'receive' '\(' posS=@ name=Name posE=@ dataExpList={ '\[' dataExp=DE '\]' }* '\)' '.' lb sp proc=SPE procMore=SPE1?
	.procType = string {
		return "receive"
	}
	| '\(' proc=SPE '\)' procMore=SPE1?
	.procType = string {
		return "brackets"
	}
	| posS=@ name=Name posE=@ '\(' dataExpFirst=DE? dataExpW={',' dataExp=DE}+ '\)' procMore=SPE1?
	.procType = string {
		return "call"
	}
	| posS=@ name=Name posE=@ procMore=SPE1?
	.procType = string {
		return "name"
	}

SPE1 := lb sp '\+' proc=SPE procMore=SPE1?
.procType = string {
		return "choice"
	}

//===============
// DATA EXPRESSIONS
//===============

DE :=
	  '{' dataExp=DE '}' dataExpMore=DE1?
	.dataExpType = string {
		return "singleton"
	}
	| '{' '\(' posS=@ name=Name posE=@ ',' dataExpLeft=DE '\)' '|' dataExpRight=DE '}' dataExpMore=DE1?
	.dataExpType = string {
		return "partial"
	}
	| '{' posS=@ name=Name posE=@ '|' dataExpRight=DE '}' dataExpMore=DE1?
	.dataExpType = string {
		return "set"
	}
	| pos=@ 'lambda ' name=Name '.' dataExp=DE dataExpMore=DE1?
	.dataExpType = string {
		return "lambda"
	}
	| pos=@ 'forall ' name=Name '.' dataExp=DE dataExpMore=DE1?
	.dataExpType = string {
		return "forall"
	}
	| pos=@ 'exists ' name=Name '.' dataExp=DE dataExpMore=DE1?
	.dataExpType = string {
		return "exists"
	}
	| '\(' dataExp=DE '\)' dataExpMore=DE1?
	.dataExpType = string {
		return "brackets"
	}
	| posS=@ name=Name posE=@ dataExpMore=DE1?
	.dataExpType = string {
		return "name"
	}

DE1 := 
	  sp dataExp=DE dataExpMore=DE1?
	  .dataExpType = string {
		return "function"
	}
	| dataExpW={',' dataExp=DE}+ dataExpMore=DE1?
	.dataExpType = string {
		return "tuple"
	}
	| '->' dataExp=DE dataExpMore=DE1?
	.dataExpType = string {
		return "implicates"
	}
	| '<->' dataExp=DE dataExpMore=DE1?
	.dataExpType = string {
		return "iff"
	}
	| '&' dataExp=DE dataExpMore=DE1?
	.dataExpType = string {
		return "and"
	}
	| '|' dataExp=DE dataExpMore=DE1?
	.dataExpType = string {
		return "or"
	}
	| '=' dataExp=DE dataExpMore=DE1?
	.dataExpType = string {
		return "eq"
	}
	| '!=' dataExp=DE dataExpMore=DE1?
	.dataExpType = string {
		return "neq"
	}
	| '>=' dataExp=DE dataExpMore=DE1?
	.dataExpType = string {
		return "gtreq"
	}
	| '<=' dataExp=DE dataExpMore=DE1?
	.dataExpType = string {
		return "leseq"
	}
	| '>' dataExp=DE dataExpMore=DE1?
	.dataExpType = string {
		return "gtr"
	}
	| '<' dataExp=DE dataExpMore=DE1?
	.dataExpType = string {
		return "les"
	}
	| func=Infix dataExp=DE dataExpMore=DE1?
	.dataExpType = string {
		return "infix"
	}


//=============
//  BASIC
//=============

// In the original grammar, some checking (through RATS functions) is done to differentiate
// between different kinds of Infix, and different kinds of Name. Currently, I have all infixes
// as 'Infix', and for Names have 'Name' and 'TypeName', which are currenlty identical. The
// grammar uses them for different things, so I'll keep them separate to make things easier later
// when I am hopefully able to split them up.

TypeName := name=Name
	.value = string {return name.value}

Name := nameString=NameString 
	.value = string {return nameString.value}

NameString := prefix={pre=NameChar* dot='\.' post=NameString} 
	.value = string {
		return prefix.pre.map(NameChar => NameChar.value).join('') + '.' + prefix.post.value
	}
	| final=NameChar+
	.value = string{
		return final.map(NameChar=> NameChar.value).join('')
	}

NameChar := char='[a-zA-Z0-9]' //simplified for now
	.value = string{return char}

Infix := char='[\*\+-<\=>\!\&|\\]+' //Currently "InfixOps"
	.value = string{return char}

ws := {sp | lb}* 						//any whitespace

sp := {' ' | '\t' | '\v'}+				//1 or more spaces/tabs

lb := {'\n' | '\r\n'}+					//1 or more linebreaks 	