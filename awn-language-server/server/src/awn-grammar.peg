AWNRoot := ws block=Block* ws $

//in a block, each entry must be separated by a linebreak, optional spacing at the start
//because each entry must be delimited with a linebreak, an extra line is required at the end of the doc currently

Block :=
	  pos=@ 'INCLUDES:' include=Include+
	| pos=@ 'include' include=Include
	| pos=@ 'TYPES:' type=Type+
	| pos=@ 'VARIABLES:' var=ConVar+
	| pos=@ 'CONSTANTS:' const=ConVar+
	| pos=@ 'FUNCTIONS:' func=Function+
	| pos=@ 'PROCESSES:' proc=Process+
	| pos=@ 'proc' proc=Process
	| pos=@ 'ALIASES:' alias=Alias+

Include := ws posS=@ name=Name posE=@ os lb

Type := ws posS=@ typeName=TypeName posE=@ typeExprW={os '\=' os typeExpr=TE }? os lb

ConVar :=
	  ws typeExpr=TE sp posS=@ nameFirst=Name posE=@ namesMore={ws ',' ws posS=@ name=Name posE=@ }* os lb .typeDeclaredFirst = boolean{return true}
	| ws posS=@ name=Name posE=@ os '\:' os typeExpr=TE os lb .typeDeclaredFirst = boolean{return false}

Function :=
	  ws posS=@ name=Name posE=@ os '\:' os typeExpr=TE os lb
	| ws posS=@ name=Infix posE=@ os '\:' os typeExpr=TE os lb

Process :=
	  ws pos1S=@ nameFirst=Name pos1E=@ os '\(' pos2S=@ argFirst=Name? pos2E=@ argsMore={',' os posS=@ name=Name posE=@ }* '\)' os '\:\=' lb sp proc=SPE os lb
	| ws posS=@ name=Name posE=@ os '\:\=' proc=SPE os lb

Alias :=
	  ws pos1S=@ nameFirst=Name pos1E=@ os '\:\=' sp pos2S=@ argFirst=Name? os pos2E=@ argsMore={',' os posS=@ name=Name posE=@ }* os lb
	| ws posS=@ name=Name posE=@ os '\:\=' sp dataExp=DE os lb

//============
// TYPE EXPRESSIONS
//============

TE := '\(' os typeExpr=TE os '\)' typeExprMore=TE1?
	.typetype = string{
		return "brackets"
	}
	| pos=@ 'Pow' '\(' os typeExpr=TE os '\)' typeExprMore=TE1?
	.typetype = string{
		return "pow"
	}
	| '\[' os typeExpr=TE os '\]' typeExprMore=TE1?
	.typetype = string{
		return "list"
	}
	| posS=@ typename=TypeName posE=@ typeExprMore=TE1?
	.typetype = string{
		return "name"
	}

TE1 := os '\->' os typeExpr=TE typeExprMore=TE1?
	.typetype = string{
		return "function"
	}
	| os '\+->' os typeExpr=TE typeExprMore=TE1?
	.typetype = string{
		return "part_function"
	}
	| products={pos=@ ' x ' typeExpr=TE}* typeExprMore=TE1?
	.typetype = string{
		return "product"
	}


//==================
// SP EXPRESSIONS
//==================

SPE :=
	  '\[' os posDES=@ dataExp=DE posDEE=@ os '\]' lb sp proc=SPE procMore=SPE1?
	.procType = string {
		return "guard"
	}
	| '\[\[' os posA=@ name=Name posB=@ os '\:\=' os posC=@ dataExpAssignment=DE posD=@ os '\]\]' lb sp proc=SPE procMore=SPE1?
	.procType = string {
		return "assignment"
	}
	| pos=@ 'unicast' '\(' posA=@ dataExpL=DE posB=@ ';' os dataExpR=DE posC=@ '\)' '.' ws procL=SPE ws '>' procR=SPE procMore=SPE1?
	.procType = string {
		return "unicast"
	} 
	| pos=@ 'broadcast' '\(' posA=@ dataExp=DE posB=@ '\)' '.' ws proc=SPE procMore=SPE1?
	.procType = string {
		return "broadcast"
	}
	| pos=@ 'groupcast' '\(' posA=@ dataExpL=DE posB=@ ';' os dataExpR=DE posC=@ '\)' '.' ws proc=SPE procMore=SPE1?
	.procType = string {
		return "groupcast"
	}
	| pos=@ 'send' '\(' posA=@ dataExp=DE posB=@ '\)' '.' ws proc=SPE procMore=SPE1?
	.procType = string {
		return "send"
	}
	| pos=@ 'deliver' '\(' posA=@ dataExp=DE posB=@ '\)' '.' ws proc=SPE procMore=SPE1?
	.procType = string {
		return "deliver"
	}
	| pos=@ 'receive' '\(' posS=@ name=Name posE=@ dataExpList={ '\[' dataExp=DE '\]' }* '\)' '.' ws proc=SPE procMore=SPE1?
	.procType = string {
		return "receive"
	}
	| '\(' ws proc=SPE ws '\)' procMore=SPE1?
	.procType = string {
		return "brackets"
	}
	| posS=@ !'unicast' !'broadcast' !'groupcast' !'send' !'deliver' !'receive' name=Name posE=@ '\(' dataExpFirst=DE? dataExpW={',' os dataExp=DE}* '\)' procMore=SPE1?
	.procType = string {
		return "call"
	}
	| posS=@ name=Name posE=@ procMore=SPE1?
	.procType = string {
		return "name"
	}

SPE1 := lb sp '\+' lb sp proc=SPE procMore=SPE1?
.procType = string {
		return "choice"
	}

//===============
// DATA EXPRESSIONS
//===============

DE :=
	'{' '\(' posS=@ name=Name posE=@ ',' dataExpLeft=DE '\)' ' | ' dataExpRight=DE '}' dataExpMore=DE1?
	.dataExpType = string {
		return "partial"
	}
	| '{' posS=@ name=Name posE=@ ' | ' dataExpRight=DE '}' dataExpMore=DE1?
	.dataExpType = string {
		return "set"
	}
	| '{' dataExp=DE '}' dataExpMore=DE1?
	.dataExpType = string {
		return "singleton"
	}
	| pos=@ 'lambda ' posS=@ name=Name '. ' dataExp=DE dataExpMore=DE1?
	.dataExpType = string {
		return "lambda"
	}
	| pos=@ 'forall ' posS=@ name=Name '. ' dataExp=DE dataExpMore=DE1?
	.dataExpType = string {
		return "forall"
	}
	| pos=@ 'exists ' posS=@ name=Name '. ' dataExp=DE dataExpMore=DE1?
	.dataExpType = string {
		return "exists"
	}
	| posN=@ name={Name | Infix} posNEnd=@ '\(' os posS=@ dataExp=DE posE=@ os '\)' dataExpMore=DE1?
	.dataExpType = string {
		return "function"
	}
	| '\(' dataExp=DE '\)' dataExpMore=DE1?
	.dataExpType = string {
		return "brackets"
	}
	| posS=@ name=Name posE=@ dataExpMore=DE1?
	.dataExpType = string {
		return "name"
	}

DE1 := 
	os posS=@ '\->' posE=@ os dataExp=DE dataExpMore=DE1?
	.dataExpType = string {
		return "implicates"
	}
	| os posS=@ '<\->' posE=@ os dataExp=DE dataExpMore=DE1?
	.dataExpType = string {
		return "iff"
	}
	| os posS=@ '\&' posE=@ os dataExp=DE dataExpMore=DE1?
	.dataExpType = string {
		return "and"
	}
	| os posS=@ '||' posE=@ os dataExp=DE dataExpMore=DE1?
	.dataExpType = string {
		return "or"
	}
	| os posS=@ '\=' posE=@ os dataExp=DE dataExpMore=DE1?
	.dataExpType = string {
		return "eq"
	}
	| os posS=@ '\!\=' posE=@ os dataExp=DE dataExpMore=DE1?
	.dataExpType = string {
		return "neq"
	}
	| os posS=@ '>\=' posE=@ os dataExp=DE dataExpMore=DE1?
	.dataExpType = string {
		return "gtreq"
	}
	| os posS=@ '<\=' posE=@ os dataExp=DE dataExpMore=DE1?
	.dataExpType = string {
		return "leseq"
	}
	| os posS=@ '>' posE=@ os dataExp=DE dataExpMore=DE1?
	.dataExpType = string {
		return "gtr"
	}
	| os posS=@ '<' posE=@ os dataExp=DE dataExpMore=DE1?
	.dataExpType = string {
		return "les"
	}
	| os posS=@ '\:' posE=@ os dataExp=DE dataExpMore=DE1?
	.dataExpType = string {
		return "col"
	}
	| os posS=@ func=Infix posE=@ os dataExp=DE dataExpMore=DE1?
	.dataExpType = string {
		return "infix"
	}
	| objects={pos=@ os ',' os dataExp=DE}* dataExpMore=DE1?
	.dataExpType = string {
		return "tuple"
	}


//=============
//  BASIC
//=============

// In the original grammar, some checking (through RATS functions) is done to differentiate
// between different kinds of Infix, and different kinds of Name. Currently, I have all infixes
// as 'Infix', and for Names have 'Name' and 'TypeName', which are currenlty identical. The
// grammar uses them for different things, so I'll keep them separate to make things easier later
// when I am hopefully able to split them up.

TypeName := name=Name
	.value = string {return name.value}

Name := nameString=NameString 
	.value = string {return nameString.value}

NameString := prefix={pre=NameChar* dot='\.' post=NameString} 
	.value = string {
		return prefix.pre.map(NameChar => NameChar.value).join('') + '.' + prefix.post.value
	}
	| final=NameChar+
	.value = string{
		return final.map(NameChar=> NameChar.value).join('')
	}

NameChar := char='[a-zA-Z0-9_]'
	.value = string{return char}

// their infix also allows colon ':', but this is incompatible because infix function definitions are ended with a colon 

Infix := char='[\*\+\=\-<>\!\&\\|]'+
	.value = string{return char.join("")}

ws := {sp | lb}* 						//any whitespace

os := {' ' | '\t' | '\v'}*				//optional spacing (within one line)

sp := {' ' | '\t' | '\v'}+				//1 or more spaces/tabs

lb := {'\n' | '\r\n'}+					//1 or more linebreaks 	