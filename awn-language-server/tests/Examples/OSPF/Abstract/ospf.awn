include types.awn
include hello.awn
include dbd.awn
include req.awn
include upd.awn

proc OSPF(ip,nbrs,lsdb,hello_t) :=
  receive(msg).
  (
    [ msg = hello(ips,sip) ]
      HELLO(ips,sip,ip,nbrs,lsdb,hello_t)
    + [ msg = dbd(lsa_hdrs,sip) ]
      DBD(lsa_hdrs,sip,ip,nbrs,lsdb,hello_t)
    + [ msg = req(lsa_hdrs,sip) ]
      REQ(lsa_hdrs,sip,ip,nbrs,lsdb,hello_t)
    + [ msg = upd(lsas,sip) ]
      UPD(lsas,sip,ip,nbrs,lsdb,hello_t)
  )
  + [ now >= hello_t ]
    [[ hello_t := now + hello_intvl ]]
    send(sndmsg(hello({pi_1(n)|isElem(n,nbrs)},ip),emptySet)).
    OSPF(ip,nbrs,lsdb,hello_t)
  + [ deadNBRS(nbrs,now) != emptySet ]
    [[ nbrs := nbrs-deadNBRS(nbrs,now) ]]
    [[ lsa := newLSA(ip,now,nbrs) ]]
    [[ lsdb := install(lsdb,{lsa}) ]]
    send(sndmsg(upd({lsa},ip),{pi_1(n)|isElem(n,nbrs)})).
    OSPF(ip,nbrs,lsdb,hello_t)
